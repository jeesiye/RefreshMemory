内部类的构造器有特殊的规则,而这一规则用户无法察觉,因为都是编译器完成插入的.  
针对于常规内部类和嵌套类,其构造器有很大的区别的.  
- 嵌套类:  
  不论其引用外部类的全局变量与否,构造器和常规类的构造器一样!  
- 常规内部类:  
  1. 成员内部类:不论引用外部的全局变量与否,构造器参数只有2个.  
  1. 局部内部类和匿名内部类分两种情况:  
     - 不引用外部的局部变量,不论引用外部的全局变量与否,构造器参数只有2个.  
     - 若引用外部的局部变量,构造器参数是`n+2`个.  

---

成员内部类:  
不论引用外部类的成员属性与否,编译器在内部类的构造器只会插入外部类这个参数.  
即内部类的构造器有两个参数,一个是隐式参数`this`,一个是外部类的签名.  
先讨论不引用外部类属性的场景:  
__实际代码__:  
```java
class OutClass {  
    class InnerClass {}
}  
```  
__反汇编验证__:  
```java
ocn.axy.run.OutClass$InnerClass(ocn.axy.run.OutClass);  
  descriptor: (Locn/axy/run/OutClass;)V  
  flags:  
  Code:  
    stack=2, locals=2, args_size=2  
       0: aload_0  
       1: aload_1  
       2: putfield      #10                 // Field this$0:Locn/axy/run/OutClass;  
       5: aload_0  
       6: invokespecial #12                 // Method java/lang/Object."<init>":()V  
       9: return  
    LineNumberTable:  
      line 6: 0  
    LocalVariableTable:  
      Start  Length  Slot  Name   Signature  
          0      10     0  this   Locn/axy/run/OutClass$InnerClass;  
```  
讨论引用外部类属性的场景:  
__实际代码__:  
```java
class OutClass {  
    private int num;  
    class InnerClass {  
        public void method() {  
            int number = num;  
        }  
    }  
}  
```  
__反汇编验证__:  
```java
ocn.axy.run.OutClass$InnerClass(ocn.axy.run.OutClass);  
  descriptor: (Locn/axy/run/OutClass;)V  
  flags:  
  Code:  
    stack=2, locals=2, args_size=2  
       0: aload_0  
       1: aload_1  
       2: putfield      #10                 // Field this$0:Locn/axy/run/OutClass;  
       5: aload_0  
       6: invokespecial #12                 // Method java/lang/Object."<init>":()V  
       9: return  
    LineNumberTable:  
      line 7: 0  
    LocalVariableTable:  
      Start  Length  Slot  Name   Signature  
          0      10     0  this   Locn/axy/run/OutClass$InnerClass;  

public void method();  
  descriptor: ()V  
  flags: ACC_PUBLIC  
  Code:  
    stack=1, locals=2, args_size=1  
       0: aload_0  
       1: getfield      #10                 // Field this$0:Locn/axy/run/OutClass;  
       4: invokestatic  #20                 // Method ocn/axy/run/  OutClass.access$0:(Locn/axy/run/OutClass;)I  
       7: istore_1  
       8: return  
    LineNumberTable:  
      line 9: 0  
      line 10: 8  
    LocalVariableTable:  
      Start  Length  Slot  Name   Signature  
          0       9     0  this   Locn/axy/run/OutClass$InnerClass;  
          8       1     1 number   I  
```  

---

局部内部类的构造器,和是否引用外部的局部变量有很大的关系的.  
- 不论引用外部全局变量与否,构造器参数只有2个;  
- 若引用外部的局部变量,构造器参数有`n+2`个;  

仅验证不引用,引用全局属性同等：  
__实际代码__:  
```java
class OutClass{
	int num = 32;
	public void method() {
		class InnerClass{
			int number = num;
		}
	}
}
```  
__反汇编验证__:  
```java
ocn.axy.run.OutClass$1InnerClass(ocn.axy.run.OutClass);
  descriptor: (Locn/axy/run/OutClass;)V
  flags:
  Code:
    stack=2, locals=2, args_size=2
       0: aload_0
       1: aload_1
       2: putfield      #12                 // Field this$0:Locn/axy/run/OutClass;
       5: aload_0
       6: invokespecial #14                 // Method java/lang/Object."<init>":()V
       9: aload_0
      10: aload_1
      11: getfield      #17                 // Field ocn/axy/run/OutClass.num:I
      14: putfield      #22                 // Field number:I
      17: return
    LineNumberTable:
      line 7: 0
      line 8: 9
      line 7: 17
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          0      18     0  this   Locn/axy/run/OutClass$1InnerClass;
```  
引用外部的局部变量:  
__实际代码__:  
```java
class OutClass {  
    public final void function() {  
        int num = 32;  
        class Innerclass {  
            int number = num;  
            public void method() {  
                int abc = num;  
            }  
        }  
    }  
}  
```  
__反汇编验证__:  
```java
ocn.axy.run.OutClass$1Innerclass(ocn.axy.run.OutClass, int);  
  descriptor: (Locn/axy/run/OutClass;I)V  
  flags:  
  Code:  
    stack=2, locals=3, args_size=3  
       0: aload_0  
       1: aload_1  
       2: putfield      #13                 // Field this$0:Locn/axy/run/OutClass;  
       5: aload_0  
       6: iload_2  
       7: putfield      #15                 // Field val$num:I  
      10: aload_0  
      11: invokespecial #17                 // Method java/lang/Object."<init>":()V  
      14: aload_0  
      15: iload_2  
      16: putfield      #20                 // Field number:I  
      19: return  
    LineNumberTable:  
      line 11: 0  
      line 12: 14  
      line 11: 19  
    LocalVariableTable:  
      Start  Length  Slot  Name   Signature  
          0      20     0  this   Locn/axy/run/OutClass$1Innerclass;  
```  

---

匿名内部类的构造器原理和局部内部类类似:  
- 不论引用外部的全局变量与否,构造器只有2个参数;  
- 若引用外部的局部变量,构造器参数会有`2+n`个;  

仅验证不引用外部的全局变量,若引用同等.  
__实际代码__:  
```java
class OutClass {  
    public void function() {  
        Intef intef = new Intef() {};
    }  
}  
interface Intef {}  
```  
__反汇编验证__:  
```java
ocn.axy.run.OutClass$1(ocn.axy.run.OutClass);  
  descriptor: (Locn/axy/run/OutClass;)V  
  flags:  
  Code:  
    stack=2, locals=2, args_size=2  
       0: aload_0  
       1: aload_1  
       2: putfield      #12                 // Field this$0:Locn/axy/run/OutClass;  
       5: aload_0  
       6: invokespecial #14                 // Method java/lang/Object."<init>":()V  
       9: return  
    LineNumberTable:  
      line 10: 0  
    LocalVariableTable:  
      Start  Length  Slot  Name   Signature  
          0      10     0  this   Locn/axy/run/OutClass$1;  
```  
验证引用外部的局部变量,产生`n+2`的效果.  
__实际代码__:  
```java
class OutClass {  
    public void function() {  
        int num = 32;  
        Intef intef = new Intef() {  
            public void method() {  
                int number = num;  
            }  
        };  
    }  
}  
interface Intef {}  
```  
__反汇编验证__:  
```java
ocn.axy.run.OutClass$1(ocn.axy.run.OutClass, int);  
  descriptor: (Locn/axy/run/OutClass;I)V  
  flags:  
  Code:  
    stack=2, locals=3, args_size=3  
       0: aload_0  
       1: aload_1  
       2: putfield      #14                 // Field this$0:Locn/axy/run/OutClass;  
       5: aload_0  
       6: iload_2  
       7: putfield      #16                 // Field val$num:I  
      10: aload_0  
      11: invokespecial #18                 // Method java/lang/Object."<init>":()V  
      14: return  
    LineNumberTable:  
      line 11: 0  
    LocalVariableTable:  
      Start  Length  Slot  Name   Signature  
          0      15     0  this   Locn/axy/run/OutClass$1;  
```  

---

嵌套类是个比较特殊的内部类,其本质上就是消除了内部类对外部类的引用资质.因此嵌套类的构造器,不论是否引用外部类的变量与否,其构造器和常规类无区别.  
(只测试一种情况,其他引用与否,都一样的结论.)  
__实际代码__:  
```java
class OutClass {  
    static class InnerClass {}
}  
```  
__反汇编验证__:  
```java
ocn.axy.run.OutClass$InnerClass();  
  descriptor: ()V  
  flags:  
  Code:  
    stack=1, locals=1, args_size=1  
       0: aload_0  
       1: invokespecial #8                  // Method java/lang/Object."<init>":()V  
       4: return  
    LineNumberTable:  
      line 9: 0  
    LocalVariableTable:  
      Start  Length  Slot  Name   Signature  
          0       5     0  this   Locn/axy/run/OutClass$InnerClass;  
```  
